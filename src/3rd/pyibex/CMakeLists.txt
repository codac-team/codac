# ==================================================================
#  codac-pyibex - cmake configuration file
# ==================================================================

  list(APPEND SRC ${CMAKE_CURRENT_SOURCE_DIR}/codac_Catan2.h
                  ${CMAKE_CURRENT_SOURCE_DIR}/codac_CtcPolar.cpp
                  ${CMAKE_CURRENT_SOURCE_DIR}/codac_CtcPolar.h
                  ${CMAKE_CURRENT_SOURCE_DIR}/codac_predef_contractors.cpp
                  ${CMAKE_CURRENT_SOURCE_DIR}/codac_predef_contractors.h
                  )


################################################################################
# Create the target for libcodac-pyibex
################################################################################

  add_library(codac-pyibex ${SRC})
  target_compile_options(codac-pyibex PUBLIC ${CODAC_CFLAGS})

  # todo: find a clean way to access codac header files?
  set(CODAC_HEADERS_DIR ${CMAKE_CURRENT_BINARY_DIR}/../../../include)
  target_include_directories(codac-pyibex PUBLIC ${CODAC_HEADERS_DIR}
                                                 ${CMAKE_CURRENT_SOURCE_DIR})
  target_link_libraries(codac-pyibex PUBLIC Ibex::ibex codac)


################################################################################
# Installation of libcodac-pyibex files
################################################################################

# Getting header files from sources

  foreach(srcfile ${SRC})
    if(srcfile MATCHES "\\.h$" OR srcfile MATCHES "\\.hpp$")
      list(APPEND HDR ${srcfile})
      # Copying header files for other Codac libraries compiled in the same time
      file(COPY ${srcfile} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../../include)
    endif()
  endforeach()

# Generates a codac-pyibex.h file

  set(CODAC_3RD_PYIBEX_MAIN_HEADER ${CMAKE_CURRENT_BINARY_DIR}/codac-pyibex.h)
  file(WRITE ${CODAC_3RD_PYIBEX_MAIN_HEADER} "/* This file is generated by CMake */\n\n")
  file(APPEND ${CODAC_3RD_PYIBEX_MAIN_HEADER} "#ifndef __CODAC_3RD_PYIBEX_H__\n#define __CODAC_3RD_PYIBEX_H__\n\n")
  foreach(header_path ${HDR})
    get_filename_component(header_name ${header_path} NAME)
    file(APPEND ${CODAC_3RD_PYIBEX_MAIN_HEADER} "#include <${header_name}>\n")
  endforeach()
  file(APPEND ${CODAC_3RD_PYIBEX_MAIN_HEADER} "\n#endif \n")
  file(COPY ${CODAC_3RD_PYIBEX_MAIN_HEADER} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../../include)

# Install files in system directories

  install(TARGETS codac-pyibex DESTINATION ${CMAKE_INSTALL_LIBDIR})
  install(FILES ${HDR} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/codac-pyibex)
  install(FILES ${CODAC_3RD_PYIBEX_MAIN_HEADER} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/codac-pyibex)