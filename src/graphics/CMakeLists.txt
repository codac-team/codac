# ==================================================================
#  Codac - cmake configuration file
# ==================================================================

  list(APPEND CODAC_GRAPHICS_SRC
    ${CMAKE_CURRENT_SOURCE_DIR}/codac2_empty.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/codac2_empty.h
  )

################################################################################
# Create the target for libcodac-graphics
################################################################################

  add_library(${PROJECT_NAME}-graphics ${CODAC_GRAPHICS_SRC})
  target_include_directories(${PROJECT_NAME}-graphics PUBLIC
    ${CMAKE_CURRENT_SOURCE_DIR}/vibes
  )
  target_link_libraries(${PROJECT_NAME}-graphics PUBLIC Ibex::ibex)
  
################################################################################
# Installation of libcodac-graphics files
################################################################################
  
# Getting header files from sources

  foreach(srcfile ${CODAC_GRAPHICS_SRC})
    if(srcfile MATCHES "\\.h$" OR srcfile MATCHES "\\.hpp$")
      list(APPEND CODAC_GRAPHICS_HDR ${srcfile})
      file(COPY ${srcfile} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../include)
    endif()
  endforeach()

# Generating the file codac-graphics.h

  set(CODAC_GRAPHICS_MAIN_HEADER ${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}-graphics.h)
  file(WRITE ${CODAC_GRAPHICS_MAIN_HEADER} "/* This file is generated by CMake */\n\n")
  file(APPEND ${CODAC_GRAPHICS_MAIN_HEADER} "#ifndef __CODAC_GRAPHICS_H__\n#define __CODAC_GRAPHICS_H__\n\n")
  foreach(header_path ${CODAC_GRAPHICS_HDR})
    get_filename_component(header_name ${header_path} NAME)
    file(APPEND ${CODAC_GRAPHICS_MAIN_HEADER} "#include <${header_name}>\n")
  endforeach()
  file(APPEND ${CODAC_GRAPHICS_MAIN_HEADER} "\n#endif /* __CODAC_GRAPHICS_H__ */\n")
  file(COPY ${CODAC_GRAPHICS_MAIN_HEADER} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../include)

# Install files in system directories

  install(TARGETS ${PROJECT_NAME}-graphics DESTINATION ${CMAKE_INSTALL_LIBDIR})
  install(FILES ${CODAC_GRAPHICS_HDR} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME}-graphics)
  install(FILES ${CODAC_GRAPHICS_MAIN_HEADER} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME}-graphics)