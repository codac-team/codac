# ==================================================================
#  Codac - cmake configuration file
# ==================================================================

  list(APPEND CODAC_GRAPHICS_SRC
    
    ${CMAKE_CURRENT_SOURCE_DIR}/3rd/ipe/codac2_Figure2D_IPE.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/3rd/ipe/codac2_Figure2D_IPE.h
    ${CMAKE_CURRENT_SOURCE_DIR}/3rd/vibes/codac2_Figure2D_VIBes.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/3rd/vibes/codac2_Figure2D_VIBes.h
    ${CMAKE_CURRENT_SOURCE_DIR}/3rd/vibes/vibes.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/3rd/vibes/vibes.h

    ${CMAKE_CURRENT_SOURCE_DIR}/figures/codac2_Figure2D.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/figures/codac2_Figure2D.h
    ${CMAKE_CURRENT_SOURCE_DIR}/figures/codac2_Figure2DInterface.h
    ${CMAKE_CURRENT_SOURCE_DIR}/figures/codac2_OutputFigure2D.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/figures/codac2_OutputFigure2D.h

    ${CMAKE_CURRENT_SOURCE_DIR}/paver/codac2_drawwhilepaving.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/paver/codac2_drawwhilepaving.h

    ${CMAKE_CURRENT_SOURCE_DIR}/styles/codac2_Color.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/styles/codac2_Color.h
    ${CMAKE_CURRENT_SOURCE_DIR}/styles/codac2_StyleProperties.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/styles/codac2_StyleProperties.h
  )

################################################################################
# Create the target for libcodac-graphics
################################################################################

  #if(NOT CMAKE_CXX_STANDARD)
    set(CMAKE_CXX_STANDARD 20)
    set(CMAKE_CXX_STANDARD_REQUIRED ON)
  #endif()

  add_library(${PROJECT_NAME}-graphics ${CODAC_GRAPHICS_SRC})
  target_include_directories(${PROJECT_NAME}-graphics PUBLIC
    ${CMAKE_CURRENT_SOURCE_DIR}/3rd/ipe
    ${CMAKE_CURRENT_SOURCE_DIR}/3rd/vibes
    ${CMAKE_CURRENT_SOURCE_DIR}/figures
    ${CMAKE_CURRENT_SOURCE_DIR}/paver
    ${CMAKE_CURRENT_SOURCE_DIR}/styles
  )
  target_link_libraries(${PROJECT_NAME}-graphics PUBLIC ${PROJECT_NAME}-core Ibex::ibex Eigen3::Eigen)
  

################################################################################
# For the generation of the PKG file
################################################################################
  
  set(CODAC_PKG_CONFIG_CFLAGS "${CODAC_PKG_CONFIG_CFLAGS} -I\${includedir}/${PROJECT_NAME}-graphics" PARENT_SCOPE)
  set(CODAC_PKG_CONFIG_LIBS "${CODAC_PKG_CONFIG_LIBS} -l${PROJECT_NAME}-graphics" PARENT_SCOPE)

  
################################################################################
# Installation of libcodac-graphics files
################################################################################
  
# Getting header files from sources

  foreach(srcfile ${CODAC_GRAPHICS_SRC})
    if(srcfile MATCHES "\\.h$" OR srcfile MATCHES "\\.hpp$")
      list(APPEND CODAC_GRAPHICS_HDR ${srcfile})
      file(COPY ${srcfile} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../include)
    endif()
  endforeach()

# Generating the file codac-graphics.h

  set(CODAC_GRAPHICS_MAIN_HEADER ${CMAKE_CURRENT_BINARY_DIR}/codac-graphics.h)
  set(CODAC_MAIN_SUBHEADERS ${CODAC_MAIN_SUBHEADERS} "codac-graphics.h" PARENT_SCOPE)
  file(WRITE ${CODAC_GRAPHICS_MAIN_HEADER} "/* This file is generated by CMake */\n\n")
  file(APPEND ${CODAC_GRAPHICS_MAIN_HEADER} "#pragma once\n\n")
  foreach(header_path ${CODAC_GRAPHICS_HDR})
    get_filename_component(header_name ${header_path} NAME)
    file(APPEND ${CODAC_GRAPHICS_MAIN_HEADER} "#include <${header_name}>\n")
  endforeach()
  file(COPY ${CODAC_GRAPHICS_MAIN_HEADER} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../include)

# Install files in system directories

  install(TARGETS ${PROJECT_NAME}-graphics DESTINATION ${CMAKE_INSTALL_LIBDIR})
  install(FILES ${CODAC_GRAPHICS_HDR} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME}-graphics)
  install(FILES ${CODAC_GRAPHICS_MAIN_HEADER} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME}-graphics)