# ==================================================================
#  Codac - cmake configuration file
# ==================================================================

  list(APPEND CODAC_UNSUPPORTED_SRC
    
    ${CMAKE_CURRENT_SOURCE_DIR}/codac2_unsupported_empty.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/codac2_unsupported_empty.h
  )

################################################################################
# Create the target for libcodac-unsupported
################################################################################

  #if(NOT CMAKE_CXX_STANDARD)
    set(CMAKE_CXX_STANDARD 20)
    set(CMAKE_CXX_STANDARD_REQUIRED ON)
  #endif()

  add_library(${PROJECT_NAME}-unsupported ${CODAC_UNSUPPORTED_SRC})
  target_include_directories(${PROJECT_NAME}-unsupported PUBLIC
    ${CMAKE_CURRENT_SOURCE_DIR}/3rd/ipe
    ${CMAKE_CURRENT_SOURCE_DIR}/3rd/vibes
    ${CMAKE_CURRENT_SOURCE_DIR}/figures
    ${CMAKE_CURRENT_SOURCE_DIR}/paver
    ${CMAKE_CURRENT_SOURCE_DIR}/styles
  )
  target_link_libraries(${PROJECT_NAME}-unsupported PUBLIC ${PROJECT_NAME}-core Ibex::ibex Eigen3::Eigen)
  

################################################################################
# For the generation of the PKG file
################################################################################
  
  set(CODAC_PKG_CONFIG_CFLAGS "${CODAC_PKG_CONFIG_CFLAGS} -I\${includedir}/${PROJECT_NAME}-unsupported" PARENT_SCOPE)
  set(CODAC_PKG_CONFIG_LIBS "${CODAC_PKG_CONFIG_LIBS} -l${PROJECT_NAME}-unsupported" PARENT_SCOPE)


################################################################################
# Installation of libcodac-unsupported files
################################################################################
  
# Getting header files from sources

  foreach(srcfile ${CODAC_UNSUPPORTED_SRC})
    if(srcfile MATCHES "\\.h$" OR srcfile MATCHES "\\.hpp$")
      list(APPEND CODAC_UNSUPPORTED_HDR ${srcfile})
      file(COPY ${srcfile} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../include)
    endif()
  endforeach()

# Generating the file codac-unsupported.h

  set(CODAC_UNSUPPORTED_MAIN_HEADER ${CMAKE_CURRENT_BINARY_DIR}/codac-unsupported.h)
  set(CODAC_MAIN_SUBHEADERS ${CODAC_MAIN_SUBHEADERS} "codac-unsupported.h" PARENT_SCOPE)
  file(WRITE ${CODAC_UNSUPPORTED_MAIN_HEADER} "/* This file is generated by CMake */\n\n")
  file(APPEND ${CODAC_UNSUPPORTED_MAIN_HEADER} "#pragma once\n\n")
  foreach(header_path ${CODAC_UNSUPPORTED_HDR})
    get_filename_component(header_name ${header_path} NAME)
    file(APPEND ${CODAC_UNSUPPORTED_MAIN_HEADER} "#include <${header_name}>\n")
  endforeach()
  file(COPY ${CODAC_UNSUPPORTED_MAIN_HEADER} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../include)

# Install files in system directories

  install(TARGETS ${PROJECT_NAME}-unsupported DESTINATION ${CMAKE_INSTALL_LIBDIR})
  install(FILES ${CODAC_UNSUPPORTED_HDR} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME}-unsupported)
  install(FILES ${CODAC_UNSUPPORTED_MAIN_HEADER} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME}-unsupported)